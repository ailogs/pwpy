#! /bin/tcsh -ef
#
# Process a hex dataset automatically. We do the best we can but
# of course many things might go wrong.
#
# Note that our interpreter is tcsh -e, so we exit automatically
# if any subcommand exits with an error status.

# Uncomment to debug the commands executed by this script
#set echo = 1

# Make sure the shell environment is set up
source `dirname $0`/hex-lib-checkenv

if ($#argv < 2) then
    echo "Usage: $0 [summary file] [analysis dir]"
    echo "or     $0 -r [analysis dir]"
    echo "E.g.:  hex-3c48-hp7-summ-23:59:59a.hexsumm 3c48-1430"
    exit 1
endif

set workdir = $2
set tstart = `date +%s`

if (x"$1" == x-r) then
    set reproc = 1

    if (! -d $workdir) then
	echo "Error: in reprocessing mode, but analysis directory $workdir does not exist"
	exit 1
    endif

    echo "autoproc rerunning at:" `date -u` >>$workdir/autoproc.log
else
    set reproc = 0
    set summary = $1

    mkdir -p $workdir
    if (-e $workdir/autoproc.log) then
	echo "Error: automated processing already run in work dir $workdir"
	echo "       (run me with -r instead of a summary to reprocess)"
	exit 1
    endif

    echo "autoproc beginning at:" `date -u` >$workdir/autoproc.log
    if ("$summary" =~ /ataarchive/*) then
	# hex-proc-create also creates this file but make sure it
	# exists in case h-p-c bails very early -- we very much want
	# to know the origin observation.
	echo "$summary" >$workdir/data-archsummpath.txt
    endif

    # Have to be a bit awkward here since we're in "-e" mode.
    if (x`sh -c "grep success $summary || true"` == x) then
	touch $workdir/autoproc.summfail
    else
	hex-proc-create $summary $workdir >>& $workdir/autoproc.log
    endif
endif

if (-f $workdir/autoproc.summfail) then
    echo "autoproc: summary is missing success indicator, not attempting reduction" \
	>>$workdir/autoproc.log
    exit 1
endif

echo "hex-proc-prep:" `date -u` >>$workdir/autoproc.log
hex-proc-prep $workdir >>&$workdir/autoproc.log
echo "hex-proc-gaussfit:" `date -u` >>$workdir/autoproc.log
hex-proc-gaussfit $workdir >>&$workdir/autoproc.log
touch $workdir/autoproc.success

set tend = `date +%s`
set minutes = `echo $tstart $tend |awk '{printf "%.3f", ($2 - $1) / 60.}'`
echo "autoproc finished successfully:" `date -u` >>$workdir/autoproc.log

if ($reproc) then
    echo "autoproc: updating analysis took $minutes minutes" >>$workdir/autoproc.log
else
    echo "autoproc: processing took $minutes minutes" >>$workdir/autoproc.log
endif

exit 0
