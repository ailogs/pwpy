#! /bin/tcsh -f
# -*- shell-script -*-
#
# Merge together the gain information for multiple hex analysis directories into a
# new one. This allows, for instance, running one fit on the observations of
# several sources or several nights' of data.

# Uncomment next line to debug the commands run by this script
#set echo = 1

# Make sure the shell environment is set up
source `dirname $0`/hex-lib-checkenv

if ($#argv < 3) then
    echo "Usage: $0 [analysisdir1] [analysisdirs...] [merged analysisdir]"
    exit 1
endif

set outdir = $argv[$#argv]
@ t = $#argv - 1
set indirs = ($argv[1-$t])

if (-e $outdir/data-sinfo.txt) then
    echo "Error: $outdir/data-sinfo.txt already exists; refusing to overwrite"
    exit 1
endif

mkdir -p $outdir

# sinfo file: if all have the same value for a key, keep it; otherwise
# leave blank. Insist on all being observed at the same frequency.

set first = 1

foreach indir ($indirs)
    set src = `grep source $indir/data-sinfo.txt |cut -d' ' -f2`
    set freq = `grep freq $indir/data-sinfo.txt |cut -d' ' -f2`
    set flux = `grep flux $indir/data-sinfo.txt |cut -d' ' -f2`

    if ($first) then
	set first = 0
	set thesrc = "$src"
	set thefreq = "$freq"
	set theflux = "$flux"
	set srcok = 1
	set fluxok = 1
    else
	if ($src != $thesrc) set srcok = 0
	if ($flux != $theflux) set fluxok = 0

	if ($freq != $thefreq) then
	    echo "Error: all input datasets must be at same frequency; $indir is not"
	    exit 1
	endif
    endif
end

echo freq $freq >$outdir/data-sinfo.txt
if ($srcok) echo source $thesrc >>$outdir/data-sinfo.txt
if ($fluxok) echo flux $theflux >>$outdir/data-sinfo.txt

# build up lists of files to merge

set ofiles = ()
set rdfiles = ()
set agfiles = ()
set rgfiles = ()
set agefiles = ()
set rgefiles = ()

foreach indir ($indirs)
    set ofiles = ($ofiles $indir/data-offsets.txt)
    set rdfiles = ($rdfiles $indir/data-rawdata.txt)
    set agfiles = ($agfiles $indir/data-absgains.txt)
    set rgfiles = ($rgfiles $indir/data-relgains.txt)
    set agefiles = ($agefiles $indir/data-absgainerrs.txt)
    set rgefiles = ($rgefiles $indir/data-relgainerrs.txt)
end

# offsets and raw data files: concatenate all of them

cat $ofiles >$outdir/data-offsets.txt
cat $rdfiles >$outdir/data-rawdata.txt

# gains-related files : we have a merge tool

hex-lib-mergegainerrtables $agfiles >$outdir/data-absgains.txt
hex-lib-mergegainerrtables $rgfiles >$outdir/data-relgains.txt
hex-lib-mergegainerrtables $agefiles >$outdir/data-absgainerrs.txt
hex-lib-mergegainerrtables $rgefiles >$outdir/data-relgainerrs.txt

# All done

exit 0
