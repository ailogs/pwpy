#! /bin/tcsh -f
#
# Conduct a hex7-like observation of the specified source.
#
# Change history:
#
# 2009/09/19 PKGW Initial version

# uncomment to debug the commands being run by this script
#set echo = 1
set me = hex-obs-single:
set inttime = 10


# Args

if ($#argv > 8) then
    set flt = ($argv)
else
    echo "Enter: inst1     freq1 bw1 inst2     freq2 bw2 src  hexmode dur(sec) [r]"
    echo "E.g.:  fx64a:fxa 3140  100 fx64c:fxa 1430  100 3c48 hp7     60"
    set flt = ($<)
endif

set inst1 = $flt[1]
set freq1 = $flt[2]
set bw1 = $flt[3]
set inst2 = $flt[4]
set freq2 = $flt[5]
set bw2 = $flt[6]
set src = $flt[7]
set hexmode = $flt[8]
set dur = $flt[9]

set restart = 0

if ($#flt > 9) then
    if ($flt[10] == r) set restart = 1
endif

if (`echo $inst1 | grep ':'` == '') then
    echo "Error: must include first instrument and group separated by a colon. Exiting!"
    exit 1
endif

if (`echo $inst2 | grep ':'` == '') then
    echo "Error: must include second instrument and group separated by a colon. Exiting!"
    exit 1
endif


if (! -x `which hex-coords-$hexmode`) then
    echo "Error: unknown hex mode $hexmode (no program hex-coords-$hexmode). Exiting!"
    exit 1
endif

set pfx = hex-$src-$hexmode
set log = $pfx-main.log
echo "$me starting at `date`" |& tee -ia $log
echo "$me args: $flt" |& tee -ia $log


# Init variables.

set lo1 = `fxconf.rb hookup $inst1 |grep LO |cut -c1` # needed to unlock at end
set lo2 = `fxconf.rb hookup $inst2 |grep LO |cut -c1`


# Setup

onintr fail

if ($restart) then
    echo "$me skipping hardware init by request" |& tee -ia $log
    goto skipinit
endif

/hcro/ata/scripts/notify-obs-blog.csh $pfx "Corr A: $inst1 $freq1 $bw1" \
    "Corr B: $inst2 $freq2 $bw2" "Source / hexmode: $src $hexmode" \
    "Scan Duration: $dur s" "Start time: `date`" "Directory: `pwd`"

hex-lib-hwsetup $inst1 $freq1 $bw1 $inst2 $freq2 $bw2 \
    $inttime $pfx $log
if ($status) goto fail


# Launch observations

skipinit:

set radec = (`radec.csh $src`)
hex-coords-$hexmode $freq1 $radec[4] $radec[5] >$pfx-coords.txt
echo "$me generating coords in mode $hexmode for $src = $radec[4] $radec[5]" |& tee -ia $log
hex-obs-nosetup $inst1 $inst2 $pfx-coords.txt $pfx $src $dur -
if ($status) goto fail


# Cleanup

echo "$me unlocking LOs" |& tee -ia $log
ataunlockserver lo$lo1 $pfx
ataunlockserver lo$lo2 $pfx
echo "$me ended successfully at `date`" |& tee -ia $log
exit 0

fail:

echo "$me unlocking LOs" |& tee -ia $log
ataunlockserver lo$lo1 $pfx
ataunlockserver lo$lo2 $pfx
echo "$me ended in failure at `date`" |& tee -ia $log
exit 1
