#! /bin/tcsh -f
#
# Prepare raw hex-style observations for further analysis by
# flagging and calibrating the data.

#set echo = 1
set newrfiflags = "subint=1"
set calcalflags = "options=sefd"
set selfcalflags = "options=amp,nosc refant=11 interval=999"

# Make sure the shell environment is set up
source `dirname $0`/hex-lib-checkenv

if ($#argv < 1) then
    set workdir = .
else
    set workdir = $1
endif

if (! -d $workdir/vis00) then
    echo "Error: analysis directory $workdir does not appear to contain any data."
    echo "       (No such directory $workdir/vis00.)"
    exit 1
endif

cd $workdir
set rest = (vis??)
set rest = (${rest[2-]})


# Try to get flux information

set tmp = `mktemp`
python >$tmp <<EOF
from miriad import VisData
t = VisData ("vis00").open ('rw')
t.next ()
print t.getVarString ('source')
t.close ()
EOF
set source = `cat $tmp`

python >$tmp <<EOF
from miriad import VisData
t = VisData ("vis00").open ('rw')
t.next ()
print int (t.getVarDouble ('freq') * 1000)
t.close ()
EOF
set freq = `cat $tmp`

echo "Read source $source, freq $freq"
set flux = `python -m calmodels $source $freq`

if ($status) then
    echo "Error looking up flux, using 1 instead."
    set flux = 1
else
    echo "Found flux = $flux Jy."
endif

cat >data-sinfo.txt <<EOF
source $source
freq $freq
flux $flux
EOF


# If a time range is specified, flag out data outside of the relevant
# time period.

if (-e data-databounds.txt) then
    set tstart = `grep tstart data-databounds.txt |cut -d' ' -f2`
    set tend = `grep tend data-databounds.txt |cut -d' ' -f2`
else
    set tstart = none
    set tend = none
endif

if ($tstart != none) then
    foreach vis (vis00 $rest)
	if (-f $vis/hexflag) then
	    echo "Dataset $vis already flagged for time bounds"
	else
	    echo uvflag flagval=f select="-time($tstart,$tend)" vis=$vis
	    uvflag flagval=f select="-time($tstart,$tend)" vis=$vis
	    touch $vis/hexflag
	endif
    end
endif


# Extract coordinate offsets in the data files and load in the RA/Dec
# offsets in arcsec for use with mselfcal

if (! -f data-offsets.txt) then
    hex-lib-calcoffsets $tstart $tend vis00 $rest >data-offsets.txt
    if ($status) then
	rm -f data-offsets.txt
	exit 1
    endif
endif

set oras = (`cut -d' ' -f1 <data-offsets.txt |awk '{print $1 * -206265}'`)
set odecs = (`cut -d' ' -f2 <data-offsets.txt |awk '{print $1 * -206265}'`)


# RFI flag everything

foreach vis (vis00 $rest)
    # Remove dra/ddec headers that will mess up the modeling of the
    # offset pointings if they're there.
    delhd in=$vis/dra >&/dev/null
    delhd in=$vis/ddec >&/dev/null

    if (! -d $vis/phoenix) then
	echo "RFI sweep dataset $vis"
	echo ">>>>>>>>>>>>>>>>>>>>"
	newrfisweep.csh vis=$vis $newrfiflags
	if ($status) exit $status
	echo "<<<<<<<<<<<<<<<<<<<<"
    endif
end


# Calibrate the on-source pointing

if (-f vis00/hexcced) then
    echo "Primary pointing dataset vis00 has already been calibrated"
else
    echo "Calibrating primary pointing dataset vis00"
    echo ">>>>>>>>>>>>>>>>>>>>"
    newcalcal.csh vis=vis00 flux=$flux $calcalflags
    if ($status) exit $status
    echo "<<<<<<<<<<<<<<<<<<<<"
    cp vis00/gains vis00/gains.orig
    touch vis00/hexcced
endif

# If all of one pol are flagged, the dataset will have nfeed=1, not
# nfeed=2, which confuses some of the gains-manipulating tools
# downstream. Patch this up, using metadata from RAPID to figure out
# which feed is the one that was retained.
hex-lib-2feedgains vis00
if ($status) exit $status

if (! -f data-calrpt.txt) then
    # TODO: we could crack the calrpt in the same way that we
    # crack the image report. I'm not quite sure why the calrpt
    # contains image info that is slightly different than what's
    # reported in imgrpt.
    cp cal-$source-$freq-maps/calrpt data-calrpt.txt
endif

if (! -f data-imginfo.txt) then
    hex-lib-crack-imgrpt <cal-$source-$freq-maps/imgrpt >data-imginfo.txt
endif


# Propagate the calibration

foreach vis ($rest)
    if (-f $vis/gains) then
	echo "Dataset $vis has been calibrated"
    else
	echo "Calibrating dataset $vis"
	gpcopy vis=vis00 out=$vis
	if ($status) exit $status
    endif
end


# Uvcat into pol-split datasets and selfcal

mkdir -p selfcal-logs
set i = 1

foreach vis (vis00 $rest)
    foreach p (x y)
	if (-d $vis.$p || -f $vis.$p.wasempty) then
	    echo "Rough-calibrated dataset $vis.$p exists or was found to be empty"
	 else
	    uvcat options=unflagged select="pol($p$p),-auto" vis=$vis out=$vis.$p
	    if ($status) exit $status

	    if (`gethd in=$vis.$p/ncorr` == 0) then
		echo "Dataset $vis.$p was empty; removing and remembering"
		rm -rf $vis.$p
		touch $vis.$p.wasempty
	    endif
	endif

	if (-d $vis.$p) then
	    if (-f $vis.$p/gains) then
		echo "Dataset $vis.$p already self-caled"
	    else
		set offset = ${oras[$i]},${odecs[$i]}
		selfcal vis=$vis.$p flux=$flux offset=$offset $selfcalflags |tee selfcal-logs/$vis.$p
		if ($status) exit $status
	    endif
	endif
    end

    @ i = $i + 1
end


# Extract SEFD data

if (! -f data-sefd.txt) then
    cp vis00/sefd data-sefd.txt
endif


# Extract antenna gains normalized to 1 at pointing center and uncerts

if (! -f data-relgains.txt) then
    hex-lib-extractgains -s -m vis00 $rest >data-relgains.txt
    if ($status) then
	rm -f data-relgains.txt
	exit 1
    endif
endif

if (! -f data-relgainerrs.txt) then
    set gefiles = ()
    set oras = (`cut -d' ' -f1 <data-offsets.txt`)
    set odecs = (`cut -d' ' -f2 <data-offsets.txt`)
    set i = 1

    mkdir -p gainerrs

    foreach vis (vis00 $rest)
	set polvis = ""
	if (-d $vis.x) set polvis = $vis.x
	if (-d $vis.y) set polvis = "$polvis $vis.y"
	if ($%polvis == 0) then
	    echo "Error: pointing $vis entirely flagged"
	    exit 1
	endif

	hex-lib-calcgainerr $polvis $flux $oras[$i] $odecs[$i] gainerrs/$vis.rel
	if ($status) exit $status
	set gefiles = ($gefiles gainerrs/$vis.rel)
	@ i = $i + 1
    end

    hex-lib-mergegainerrtables $gefiles >data-relgainerrs.txt
    if ($status) then
	rm -f data-relgainerrs.txt
	exit 1
    endif
endif
    
# Extract absolute antenna gains at the pointing center and uncerts

if (! -f data-absgains.txt) then
    hex-lib-extractgains -j vis00 >data-absgains.txt
    if ($status) then
	rm -f data-absgains.txt
	exit 1
    endif
endif

if (! -f data-absgainerrs.txt) then
    hex-lib-calcgainerr vis00 $flux 0 0 data-absgainerrs.txt
    if ($status) then
	rm -f data-absgainerrs.txt
	exit 1
    endif
endif

# TODO: extract closure information of primary pointing.

exit 0
