#! /usr/bin/tcsh -f
# Calibrator Observing and Measurement Processing 
#         for Array Statistics and Status       
#
# This is going to be the cal observation program. Put simply, it will be
# awesome. Oh yes, it WILL be awesome.

set vis = $1
set times = ($2 $3)
set corr = `echo $4 |cut -d: -f1`

set marker = (`echo $times[1] | tr '[a-z]' '[A-Z]' | sed 's/\([0-9][0-9]\)\([A-Z][A-Z][A-Z]\)\([0-9][0-9]\):/\3 \2 \1 /'`)
set marker = (`date -d "$marker +0000" +%Y" "%m" "%d" "%H%M%S`)


# Collect some info from the data, using defaults

set listdata = `mktemp`
uvlist vis=$vis select="time($times[1],$times[2])" options=var,full >$listdata

set cal=`egrep '(^| )source *:' $listdata |sed -e 's/.*source *: *\([^ ]*\).*/\1/'`
set freq=`egrep '(^| )freq *:' $listdata |sed -e 's/.*freq *: *\([^ ]*\).*/\1/'`
set nchan=`egrep '(^| )nchan *:' $listdata |sed -e 's/.*nchan *: *\([^ ]*\).*/\1/'`
set sdf=`egrep '(^| )sdf *:' $listdata |sed -e 's/.*sdf *: *\([^ ]*\).*/\1/'`

set mfreq = `echo $freq | awk '{print int($1*1000)}'`
echo "Observation parameters: cal=$cal freq=$freq mfreq=$mfreq nchan=$nchan sdf=$sdf"
if (!($%cal && $%freq && $%nchan && $%sdf)) then
    echo COMPASS-AUTOFLAG `date`: "missing vars in dataset; see $listdata."
    exit 1
endif

rm -f $listdata


# Set up processing directory

set histview = /ataarchive/$marker[1]/$marker[2]/$marker[3]
set histview = $histview/status/compass-autoflag-$marker[4]/$corr-$mfreq
mkdir -p $histview || exit 1
cd $histview || exit 1


# Copy data for processing

echo "uvaver vis=$vis select=time($times[1],$times[2]) ..." >>dataread
uvaver vis=$vis select="time($times[1],$times[2])" \
    options=relax,nocal,nopass,nopol out=caldata \
    |& tee -ia dataread

if ($status) then
    rm -rf caldata
    echo "Data read failed!" >> dataread
    echo COMPASS-AUTOFLAG `date`: "couldn't copy data for analysis; giving up on this scan."
    exit 1
endif


# Flag

uvflag vis=caldata flagval=u |tee -ia autoflag.log
~/mmm/dwhysong/pigss/autoflag/autoflag cal=caldata \
    options=ata,nonoise |tee -ia autoflag.log
uvflag vis=caldata flagval=f edge=100,100,3 |tee -ia autoflag.log


# Calibrate

set calvals = (`calinfo target=$cal freq=$freq options=auto`)
if ("$calvals[1]" == "FATAL") then
    rm -rf caldata
    echo COMPASS-AUTOFLAG `date`: "data not found about the calibrator ($cal $freq)! Giving up on this scan."
    exit 1
endif

set flux = "$calvals[1],$calvals[2],$calvals[3]"
set addflux = "$calvals[8]"
set sysflux = `echo $freq $sdf $nchan | awk '{print (4+($1/5)^2)*sqrt(0.10486/($2*$3))}'`
set check = `echo $sysflux $calvals[1] |awk '{if ($1 > $2) print 1; else print 0}'`
if ($check == 1) then
    echo "NOTICE: estimating lots of phase noise, setting limit loosely"
    set plim = 60
else
    set plim = `calc "asin($sysflux/$calvals[1])*180/pi"`
endif

newcalcal.csh vis=caldata sysflux=$sysflux addflux=$addflux \
    flux=$flux plim=$plim options=polsplit,sefd \
    |tee -ia newcalcal.log


# All done.
# TODO: generate image png for comparison to newrfisweep images. And other states.

rm -rf caldata
exit 0
